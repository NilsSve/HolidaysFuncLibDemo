Use DFAllEnt.pkg
Use cCJStandardCommandBarSystem.pkg
Use cCJCommandBarSystem.pkg
Use cDigitalSoftwareCertificate.pkg

// If Is$GraphicsLibrary is defined, then the "DataFlex Graphics Library" needs to be used as a library.
// Else the "Flags" object will not be used/visible in the application.
// NOTE: The Studio will complain if the library is defined in the .sws file, but
//       the library is not accessible. Don't think there's a way around that problem.
//       For that reason the .sws files does have a link to the library.
//Define Is$GraphicsLibrary

Object oDigitalSoftwareCertificate is a cDigitalSoftwareCertificate
    Move Self to ghoDigitalSoftwareCertificate

    // Set pbWarnIfNotDigitallySigned = True if you like to have a warning when the
    // program is started, but the program has not been digitally signed.
    // Only applies when run outside the Studio.
    Set pbWarnIfNotDigitallySigned to False

    // This message does the actual checking for a valid digital certificate.
    // If the program has been tampered with e.g. it has been infected by a virus,
    // the user will get a warning and the program will be aborted.
//    Send ValidateProgramOnStartup
End_Object

Object oHtmlHelp is a cHtmlHelp
End_Object

Use cDataBaseFunctions.pkg

Object oApplication is a cApplication
    Set peHelpType to htHtmlHelp
    Set pbEnterKeyAsTabKey to True
    Set pbPreserveEnvironment to True
    Set psCompany to "RDC Tools International"
    Set psProduct to "Holidays and Other Special Dates Library"
    Set psVersion to (String(piVersionMajor(phoVersionInfo(Self))) + "." + String(piVersionMinor(phoVersionInfo(Self))))

    Property String psGridRowBackgroundColor
    // Check that the 'correct' collating.cfg is used when the program is
    // started. The data in the database was entered with an english (standard)
    // collating sequence. If another df_collate.cfg is encountered at runtime,
    // all data tables will automatically be reindexed, prior to running the program.
    // Note: We cannot use the event OnCreate for this, because the psDataPath
    // et. al. is set after that event. Instead we augment the
    // predefined cApplication's DoOpenworkSpace message.
    Procedure DoOpenWorkspace String sWorkspace
        Forward Send DoOpenWorkspace sWorkspace
        Open Nations
        Send CheckCollatingSequence of ghoDatabaseFunctions File_Field Nations.ISO_Code

#IF (!@ > 240)
        UBigInt ullUnixUTC
        DateTime dtCompileTime
        Move COMP$TIMESTAMP to ullUnixUTC 
        Move 1/1/1970 to dtCompileTime
        Move (DateAddSecond(dtCompileTime, ullUnixUTC)) to dtCompileTime
#ENDIF
    End_Procedure

End_Object

Use cRDCTooltipController.pkg
Object oToolTipController is a cRDCTooltipController
End_Object

Use oEditContextMenu.pkg
Use oDEOEditContextMenu.pkg

Object oMain is a Panel
    Set Label to (psProduct(ghoApplication))
    Set Location to 4 3
    Set Size to 356 589
    Set Locate_Mode to Center_On_Screen
    Set Icon to "NationalHolidays.ico"
    
    Property Handle phoViewMenu 0
    Property Handle phoReportMenu 0

    If ghoStatusPanel Begin
        Send Stop_StatusPanel of ghoStatusPanel     // Stops status panel if a re-index took place prior starting the appl.
    End

    Object oCommandBarSystem is a cCJCommandBarSystem
        Set peRestoreLayout to rlNoSaveRestore // rlSaveRestoreAll
        Set pbTimerUpdate to True
        Set pbAutoResizeIcons to True
        Set pbTabbedWorkspaces to True
        Set pbLargeIcons to True
#IF (!@ > 240)
        Set peVisualTheme to xtpThemeNativeWindows11 
#ELSE
        Set peVisualTheme to xtpThemeOffice2016Word
#ENDIF
        Procedure OnCreateCommandBars
            Handle hoOptions
            Get OptionsObject to hoOptions
            Forward Send OnCreateCommandBars 
            Send DisplayTabWorkspace
        End_Procedure

        Procedure DisplayTabWorkspace
            Variant vTab vPaintManager
            Handle hoTab hoPaintManager hoOptions
            Integer iSize

            If (not(IsComObjectCreated(Self))) Begin
                Procedure_Return
            End

            Get OptionsObject to hoOptions

            // Create the tab workspace object. It will get created for the Client_Area so
            // each view will be displayd on a separate tab-page (instead of the standard VDF MDI-interface)
            Get Create (RefClass(cCJTabWorkspace)) to hoTab
            Get ComShowTabWorkspace True to vTab
            // Connect the CodeJock side to the DataFlex proxy object.
            Set pvComObject of hoTab to vTab        

            // Create a paint manager object that is needed to set some
            // of the tab-workspace properties.
            Get Create (RefClass(cCJTabPaintManager)) to hoPaintManager
            Get ComPaintManager of hoTab          to vPaintManager
            Set pvComObject     of hoPaintManager to vPaintManager
            
            // Don't show the tab-pages "close" and next/prev buttons. 
            Set ComFlags of hoTab to xtpWorkspaceHideAll
            Send ComEnableGroups of hoTab

            Move 24 to iSize
            Send ComSetIconSize of hoPaintManager iSize iSize   // Set icon size for Tab-workspace.
            // To show or not to show tab-page icons...
            Set ComShowIcons    of hoPaintManager to True
            
            // Sets the tab-pages to display on the left side, instead of at the top:
            //Set ComPosition     of hoPaintManager to xtpTabPositionLeft

            // This will truncate the middle part of long items
            //Set ComDrawTextPathEllipsis of hoPaintManager to True

            //Get piToolbarIconSize of ghoApplication to iSize
            Move 32 to iSize
            Send ComSetIconSize     of hoOptions False iSize iSize  // Set icon size for Toolbar buttons.
            //Set piToolbarIconSize to iSize
            Set ComToolTipBehaviour of hoPaintManager to xtpTabToolTipAlways // We _must_ have this for tooltips to show on tab-pages (views)
        End_Procedure

        Object oMenuBar is a cCJMenuBar

            Object oFileMenu is a cCJMenuItem
                Set peControlType to xtpControlPopup
                Set psCaption to "&File"
                Set psDescription to "Find, Save, Delete, Clear information or quit this application."
                Set psCategory to "Menus"

                Object oClearMenuItem is a cCJClearMenuItem
                    Set pbAddToDesignerMenu to True
                End_Object

                Object oClearAllMenu is a cCJClearAllMenuItem
                    Set pbAddToDesignerMenu to True
                End_Object

                Object oPromptMenuItem is a cCJPromptMenuItem
                    Set pbAddToDesignerMenu to True
                    Set pbControlBeginGroup to True
                End_Object

                Object oFindMenuItem is a cCJFindMenuItem
                    Set pbAddToDesignerMenu to True
                    Set pbControlBeginGroup to True
                End_Object

                Object oFindNextMenu is a cCJFindNextMenuItem
                    Set pbAddToDesignerMenu to True
                End_Object

                Object oFindPreviousMenu is a cCJFindPreviousMenuItem
                    Set pbAddToDesignerMenu to True
                End_Object

                Object oFindFirstMenu is a cCJFindFirstMenuItem
                    Set pbAddToDesignerMenu to True
                End_Object

                Object oFindLastMenu is a cCJFindLastMenuItem
                    Set pbAddToDesignerMenu to True
                End_Object

                Object oSaveMenuItem is a cCJSaveMenuItem
                    Set pbAddToDesignerMenu to True
                    Set pbControlBeginGroup to True
                End_Object

                Object oDeleteMenuItem is a cCJDeleteMenuItem
                    Set pbAddToDesignerMenu to True
                End_Object

                Object oExitMenu is a cCJExitMenuItem
                    Set pbControlBeginGroup to True
                End_Object

            End_Object

            Object oViewMenu is a cCJMenuItem
                Set peControlType to xtpControlPopup
                Set psCaption to "&View"
                Set psToolTip to "View"
                Set psDescription to "Available Views"

                Object oDateFunctionsTestMenuItem is a cCJMenuItem
                    Set psCaption to "Date Functions Test"
                    Set psDescription to "Holidays and Other Special Dates Functions Test"
                    Set peControlStyle to xtpButtonCaptionAndDescription
                    Set psImage to "Calendar.ico"

                    Procedure OnExecute Variant vCommandBarControl
                        Handle hoClient
                        Get Client_Id to hoClient
                        Send Activate_oDateFunctionsTest of hoClient
                    End_Procedure
                End_Object

                Object oSimpleHolidaysMenuItem is a cCJMenuItem
                    Set psCaption to "Calendar"
                    Set psDescription to "Sample on using the Holiday Function Library"
                    Set peControlStyle to xtpButtonCaptionAndDescription
                    Set psImage to "NationalHolidays.ico"

                    Procedure OnExecute Variant vCommandBarControl
                        Handle hoClient
                        Get Client_Id to hoClient
                        Send Activate_oHolidays of hoClient
                    End_Procedure
                End_Object

                Object oNationsViewMenuItem is a cCJMenuItem
                    Set psCaption to "Nations Data"
                    Set psDescription to "Data about all nations in the world"
                    Set peControlStyle to xtpButtonCaptionAndDescription
                    Set psImage to "Nations.ico"

                    Procedure OnExecute Variant vCommandBarControl
                        Handle hoClient
                        Get Client_Id to hoClient
                        Send Activate_oNations of hoClient
                    End_Procedure
                End_Object

                // for testing we define the view and report menus for auto activate
                Delegate Set phoViewMenu to Self
            End_Object

            Object oNavigateMenu is a cCJMenuItem
                Set peControlType to xtpControlPopup
                Set psCaption to "&Navigate"
                Set psTooltip to "Navigate"
                Set psDescription to "Move to different areas of the application"

                Object oNextAreaMenu is a cCJNextAreaMenu
                End_Object

                Object oPriorAreaMenu is a cCJPriorAreaMenu
                End_Object

                Object oNextViewMenu is a cCJNextViewMenu
                End_Object

                Object oPriorViewMenu is a cCJPriorViewMenu
                End_Object

                Object oPromptMenu is a cCJPromptMenuItem
                    Set pbControlBeginGroup to True
                End_Object

                Object oZoomMenu is a cCJZoomMenuItem
                End_Object

            End_Object

            Object oWindowMenu is a cCJMDIWindowsMenuItem
                Set peControlType to xtpControlPopup
                Set psCaption to "&Window"
                Set psToolTip to "Window"
                Set psDescription to "Display Current Views and set other display options."

                // These are the static windows items. More will be created in onPopupInit
                Object oDisplayOptionsMenu is a cCJMenuItem
                    Set peControlType to xtpControlPopup
                    Set psCaption to "&Display Options"
                    Set psToolTip to "Display Options"
                    Set psDescription to "Set display options"

                    Object oStatusbarMenu is a cCJStatusbarMenuItem
                    End_Object

                    Object oRestoreMenusMenu is a cCJRestoreMenusMenuItem
                        Set pbControlBeginGroup to True
                    End_Object

                End_Object

            End_Object

            Object oHelpMenu is a cCJMenuItem
                Set peControlType to xtpControlPopup
                Set psCaption to "&Help"
                Set psDescription to "Access Information for learning and using this DataFlex application."
                Set psToolTip to "Help"

                Object oAboutMenuItem is a cCJAboutMenuItem
                End_Object 
                
                Object oISOCodesMenuItem is a cCJMenuItem
                    Set psCaption to "Official ISO Country Codes"
                    Set psImage to "Nations.ico"

                    Procedure OnExecute Variant vCommandBarControl
                        Runprogram Shell Background "https://www.iso.org/obp/ui/#search/code/"    
                    End_Procedure
                End_Object 
                
                Object oHelpMenuItem is a cCJHelpMenuItem
                    Procedure OnExecute Variant vCommandBarControl
                        String sHelpPath sHelpFile
                        Get psHelpPath of (phoWorkspace(ghoApplication)) to sHelpPath
                        Move "ClassReference.htm" to sHelpFile
                        Runprogram Shell Background (sHelpPath + "\" + sHelpFile)
                    End_Procedure
                End_Object

            End_Object

        End_Object

        Object oFindToolBar is a cCJToolbar
            Set psTitle to "Finding Toolbar"

            Object oFindFirstTool is a cCJFindFirstMenuItem
            End_Object

            Object oFindPreviousTool is a cCJFindPreviousMenuItem
            End_Object

            Object oFindMenuTool is a cCJFindMenuItem
            End_Object

            Object oFindNextTool is a cCJFindNextMenuItem
            End_Object

            Object oFindLastTool is a cCJFindLastMenuItem
            End_Object

            Object oPromptToolItem is a cCJPromptMenuItem
                Set pbControlBeginGroup to True
            End_Object

        End_Object

        Object oFileToolBar is a cCJToolbar
            Set psTitle to "Data Entry Toolbar"

            Object oClearToolItem is a cCJClearMenuItem
                Set peControlStyle to xtpButtonIconAndCaption
            End_Object

            Object oClearAllToolItem2 is a cCJClearAllMenuItem
                Set peControlStyle to xtpButtonIconAndCaption
            End_Object

            Object oSaveToolItem is a cCJSaveMenuItem
                Set peControlStyle to xtpButtonIconAndCaption
                Set pbControlBeginGroup to True
            End_Object

            Object oDeleteToolItem is a cCJDeleteMenuItem
                Set peControlStyle to xtpButtonIconAndCaption
            End_Object

        End_Object

        Object oEditToolBar is a cCJToolbar
            Set psTitle to "Edit Toolbar"

            Object oCutToolbarItem is a cCJCutMenuItem
            End_Object

            Object oCopyToolbarItem is a cCJCopyMenuItem
            End_Object

            Object oPasteToolbarItem is a cCJPasteMenuItem
            End_Object

            Object oDeleteEditToolbarItem is a cCJDeleteEditMenuItem
                Set pbControlBeginGroup to True
            End_Object

        End_Object

        Object oExit_Toolbar is a cCJToolbar

            Object oAboutMenuItem is a cCJAboutMenuItem
                Set peControlStyle to xtpButtonIcon
            End_Object

            Object oKoFi_MenuItem is a cCJMenuItem
                Set psCaption to "Ko-fi"
                Set psToolTip to "Bye me a coffee, to support me"
                Set psDescription to "Bye me a coffee"
                Set psImage to "Ko-fi.ico"
                Set pbControlBeginGroup to True
                Procedure OnExecute Variant vCommandBarControl
                    Runprogram Shell Background "https://ko-fi.com/nilss"
                End_Procedure
            End_Object

            Object oISOCodesMenuItem is a cCJMenuItem
                Set psCaption to "Official ISO Country Codes"
                Set psToolTip to "Official ISO Country Codes"
                Set psImage to "Nations.ico"

                Procedure OnExecute Variant vCommandBarControl
                    Runprogram Shell Background "https://www.iso.org/obp/ui/#search/code/"    
                End_Procedure
            End_Object 
                
            Object oHelpMenuItem is a cCJHelpMenuItem
                Procedure OnExecute Variant vCommandBarControl
                    String sHelpPath sHelpFile
                    Get psHelpPath of (phoWorkspace(ghoApplication)) to sHelpPath
                    Move "ClassReference.htm" to sHelpFile
                    Runprogram Shell Background (sHelpPath + "\" + sHelpFile)
                End_Procedure
            End_Object

            Object oExitMenuItem is a cCJExitMenuItem
                Set psImage to "ActionExit.ico"
                Set psToolTip to "Exit"
                Set psDescription to "Exit the program (Alt+F4)"
                Set pbControlBeginGroup to True
            End_Object

        End_Object

        Object oStatusBar is a cCJStatusBar

            Procedure OnUpdate
                Handle hoServer
                Boolean bHasRecord bChanged bHasIndex bIsDeo

                Forward Send OnUpdate
                Get DEOInformation (&hoServer) (&bHasRecord) (&bChanged) (&bHasIndex) to bIsDeo
                If (not(bIsDEO and hoServer and bChanged)) Begin
                    Set psText of oStatusPaneChanged to ""
                End
                Else Begin
                    Set psText of oStatusPaneChanged to "Changed*"
                End
            End_Procedure

            Object oStatusPane1 is a cCJStatusBarPane
                Set piID to sbpIDIdlePane
                Set pbStyleStretch to True
            End_Object

            Object oStatusPane2 is a cCJStatusBarPane
                Set phoViewPane to Self
                Set pbStyleStretch to True
            End_Object

            Object oStatusPaneChanged is a cCJStatusBarPane
            End_Object

        End_Object

        // Setup actions for Ctrl+Tab and Shift+Ctrl+Tab - switch between views.
        Object oNextTabAction is a cCJAction
            Set piShortCutKey to xtpKey_Ctrl VK_TAB

            Procedure OnExecute Variant vCommandBarControl
                Handle hoCommandBars hoClientArea
                Forward Send OnExecute vCommandBarControl
                Get CommandBarSystemObject to hoCommandBars
                Get ClientAreaObject of hoCommandBars to hoClientArea
                If hoClientArea Begin
                    Send Switch_Prior_View of hoClientArea
                End
            End_Procedure

        End_Object

        Object oPreviousTabAction is a cCJAction
            Set piShortCutKey to (xtpKey_Shift + xtpKey_Ctrl) VK_TAB

            Procedure OnExecute Variant vCommandBarControl
                Handle hoCommandBars hoClientArea
                Forward Send OnExecute vCommandBarControl
                Get CommandBarSystemObject to hoCommandBars
                Get ClientAreaObject of hoCommandBars to hoClientArea
                If hoClientArea Begin
                    Send Switch_Next_View of hoClientArea
                End
            End_Procedure

        End_Object

    End_Object

    Object oClientArea is a ClientArea
//        Use Dd_debug.dg // This Provides access to dd debugger by pressing ctrl+d within any view. Good for debugging
        Use StdAbout.pkg

        Use Nations.vw
        Use Holidays.vw
        Use DataFunctionsTest.vw
        
        Procedure Activate_About
            Send DoAbout "" "" "" "" "World64x64.bmp" "E-mail: mailto:support@rdctools.com" "This is a FREE program. Please make a contribution if you find it useful. Just click the toolbar button to make a small donation." ("This tool was created with DataFlex" * String(C_DFVersion) * "from Data Access Worldwide. Please visit http://www.dataaccess.com for a free personal copy of DataFlex.")
        End_Procedure

    End_Object

    // We do not allow closing of a view - it just doesn't make
    // sense with a tabbed interface.
    Function Exit_Loss_Confirmation For cUIObject Returns Integer
        Function_Return 1
    End_Function

    Function Verify_Exit_Application Returns Integer
        Function_Return 0
    End_Function

    // For some weird reason setting the program icon here is needed for it to
    // appear in Windows taskbar.
    Procedure End_Construct_Object
        Forward Send End_Construct_Object
        Set Icon to "NationalHolidays.ico"
    End_Procedure

End_Object

Start_UI
