Use Dfpanel.pkg
Use cApplication.pkg

Use An_Calendar.sl

Object oApplication is a cApplication
    Set peHelpType to htNoHelp  // htHtmlHelp
End_Object

// Little trick to avoid the Studio claiming child objects cannot be modelled
{ DesignerClass = cDTView }
{ OverrideProperty = Location InitialValue = 10,0 }
Class cPanel is a Panel
End_Class

Object oMain is a cPanel
    Set Location to 0 0

    Set Border_Style to Border_Dialog
    Set size to 85 126

    Set Label to "Calendar Prompt Test"
    Set Icon to "Application.ico"
    Set Maximize_Icon to False

    Object oDateForm is a Form
        Set Size to 13 80
        Set location to 15 23
        Set Form_Datatype to Mask_Date_Window
        Set Prompt_Object to oDatePrompt
        Set Prompt_Button_Mode to PB_PromptOn
        Set Form_Button_Bitmap 0 to "Combo.bmp"          // make it look like a comboform
        On_Key Key_Alt+Key_Down_Arrow  Send Prompt       // make it act like a comboform
        On_Key Key_Up_Arrow            Send prev_day
        On_Key Key_Ctrl+Key_Up_Arrow   Send first_day    // "ctrl extends"
        On_Key Key_Down_Arrow          Send next_day
        On_Key Key_Ctrl+Key_Down_Arrow Send last_day
        On_Key Key_PgUp                Send prev_month
        On_Key Key_Ctrl+Key_PgUp       Send prev_year
        On_Key Key_PgDn                Send next_month
        On_Key Key_Ctrl+Key_PgDn       Send next_year
        On_Key Key_Shift+Key_Home      Send go_today     // "shift modifies"
        Procedure prev_day
            Set Value to (Date(Value(Self) - 1))
        End_Procedure
        Procedure first_day
            Set Value to (Date(DateSetDay(Value(Self),1)))
        End_Procedure
        Procedure next_day
            Set Value to (Date(Value(Self) + 1))
        End_Procedure
        Procedure last_day
            Set Value to (Date(DateGetLastDayOfMonth(Value(Self))))
        End_Procedure
        Procedure prev_month
            Set Value to (Date(DateChangeMonth(Value(Self),-1)))  // add the parameter 'True' if you want eg 28 Feb to become 31 Jan
        End_Procedure
        Procedure next_month
            Set Value to (Date(DateChangeMonth(Value(Self),1)))
        End_Procedure
        Procedure prev_year
            Set Value to (Date(DateChangeYear(Value(Self),-1)))
        End_Procedure
        Procedure next_year
            Set Value to (Date(DateChangeYear(Value(Self),1)))
        End_Procedure
        Procedure go_today
            Set Value to (Date(CurrentDateTime()))
        End_Procedure
    End_Object

    Object OkBtn is a Button
        Set Location to 45 37
        Set Label to "OK"
        Set Default_State to True
        Procedure OnClick
            Send Close_Panel
        End_Procedure
    End_Object

End_Object

Start_UI

//End of file/////////////////////////////////////////////////////////////////